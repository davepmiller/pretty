plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    jcenter()
}

def getVersionName =  { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
        ignoreExitValue = true
    }

    return stdout.toString().trim()
}

version getVersionName()

dependencies {
    testImplementation 'junit:junit:4.12'
}

test {
    testLogging.events "passed", "skipped", "failed", "standardOut", "standardError"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publish = true
    publications = ['PrettyPublication']
    pkg {
        name = 'pretty'
        repo = 'laser'
    }
}

publishing {
    publications {
        PrettyPublication(MavenPublication) {
            from components.java
            groupId 'org.laser'
            artifactId 'pretty'
            version getVersionName()
        }
    }
}
